buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

if (!hasProperty('repo')) {
    ext.repo = 'development'
}

mainClassName = 'edu.wpi.first.tableviewer.TableViewer'

publishing {
    publications {
        maven(MavenPublication) {
            artifact(shadowJar) {
                classifier null
            }
            groupId 'edu.wpi.first.wpilib.networktables'
            artifactId 'OutlineViewer'
            version '1.0.0-SNAPSHOT'
        }
    }
    repositories.maven {
        url = "${System.getProperty('user.home')}/releases/maven/$repo"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url = "${System.getProperty('user.home')}/releases/maven/$repo"
    }
    mavenLocal()
    maven {
        url = "http://first.wpi.edu/FRC/roborio/maven/$repo"
    }
}

dependencies {
    compile "edu.wpi.first.wpilib.networktables.java:NetworkTables:3.0.0-SNAPSHOT:desktop"
    compile 'uk.gov.nationalarchives.thirdparty.netbeans:org-netbeans-swing-outline:7.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}