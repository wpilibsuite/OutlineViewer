import groovy.xml.XmlUtil

buildscript {
    dependencies {
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.0'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'checkstyle'
    id 'idea'
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '1.1'
}
apply plugin: "jacoco"
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'javafx-gradle-plugin'

//publishing {
//    publications {
//        maven(MavenPublication) {
//            artifact(shadowJar) {
//                classifier null
//            }
//            groupId 'edu.wpi.first.wpilib.networktables'
//            artifactId 'OutlineViewer'
//            version WPILibVersion.version
//        }
//    }
//}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '21.0'
    compile "edu.wpi.first.wpilib.networktables.java:NetworkTables:+:desktop"

    testCompile group: 'com.google.guava', name: 'guava-testlib', version: '21.0'
    testCompile "org.testfx:testfx-core:4.0.+"
    testCompile "org.testfx:testfx-junit:4.0.+"
    testRuntime group: 'org.testfx', name: 'openjfx-monocle', version: '1.8.0_20'
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    toolVersion = '6.19'
}

pmd {
    consoleOutput = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files(new File(rootDir, "pmd-ruleset.xml"))
    ruleSets = []
}

findbugs {
    sourceSets = [sourceSets.main, sourceSets.test]
    excludeFilter = new File(rootDir, "findBugsSuppressions.xml")
    effort = "max"
}

ext.printReportSafe = { xmlReport ->
    if (xmlReport.exists()) {
        def bugs = (new XmlParser().parse(xmlReport)).BugInstance
        bugs.each { System.out.println(new XmlUtil().serialize(it)) }
    }
}

task findbugsMainReport {
    doLast {
        printReportSafe(findbugsMain.reports.getXml().destination)
    }
}

task findbugsTestReport {
    doLast {
        printReportSafe(findbugsTest.reports.getXml().destination)
    }
}

findbugsMain.finalizedBy findbugsMainReport
findbugsTest.finalizedBy findbugsTestReport

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    testLogging {
        if (project.hasProperty('logTests') || project.hasProperty('jenkinsBuild')) {
            events "started", "passed", "skipped", "failed"
        } else {
            events "failed"
        }
        exceptionFormat "full"
    }
}

jfx {
    jfxMainAppJarName = 'OutlineViewer.jar'
    mainClass = 'edu.wpi.first.outlineviewer.OutlineViewer'
}

/*
 * Allows you to run the UI tests in headless mode by calling gradle with the -Pheadless argument
 */
if (project.hasProperty('jenkinsBuild') || project.hasProperty('headless')) {
    println "Running UI Tests Headless"
    test {
        jvmArgs = ['-Djava.awt.headless=true', '-Dtestfx.robot=glass', '-Dtestfx.headless=true', '-Dprism.order=sw', '-Dprism.text=t2k']
    }
}

mainClassName = jfx.mainClass

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
